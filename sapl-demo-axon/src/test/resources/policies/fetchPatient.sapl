/*
 * Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
 */
import filter.*

/*
 * In each SAPL document, the top level policy or policy set MUST have a unique name.
 */
set "fetch patient policy set"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * scope the policy set to be applicable to all authorization subscriptions "Fetch" actions on "patient".
 */
for resource.type == "patient" & action == "Fetch"


/*
 * All doctors have full access to medical data of the patients.
 * All nurses working in the same ward where the patient is hospitalized have full access. 
 */
policy "permit doctors and ward nurses full patient access" 
permit subject.position == "DOCTOR" | (subject.position == "NURSE" & subject.assignedWard == resource.value.ward)


/*
 * All other authenticated staff members have limited access.
 * Upon access, an event must be dispatched to allow auditing of access of the patient data.
 * Also, the medical information must be blackened.  
 */
policy "authenticated users may see filtered" 
permit subject != "anonymous"
obligation "dispatch access attempt event"
transform 
		 // Subtractive template with filters removing content
		 resource.value |- 	{ 
								@.latestIcd11Code 			: blacken(2,0,"\u2588"),
								@.latestDiagnosisText 		: blacken(2,0,"\u2588")												
		             		}


/*
 * The following policy is optional with the overall PDP configuration of the demo. 
 * It may however be necessary with different scenarios.
 * If this policy is omitted, one relies of implicit access denial by the absence of other decisions.
 * Having it present makes it explicit.
 */
policy "deny patient access if no other rule applies" 
deny
obligation "dispatch access attempt event"
