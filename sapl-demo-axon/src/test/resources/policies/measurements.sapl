/*
 * Import the filter library, so that 'blacken' can be used directly instead of using the absolute name 'filter.blacken'.
 */
import filter.*
import time.*

/*
 * In each SAPL document, the top level policy or policy set MUST have a unique name.
 */
set "fetch vital sign"

/*
 * The 'first-applicable' combination algorithm is used here in oder to avoid 'transformation uncertainty',
 * i.e., multiple policies which return PERMIT but do not agree about transformation of the resource.
 * This algorithm evaluates policies from top to bottom in the document and stops as soon as one policy 
 * yields an applicable result or errors.
 */
first-applicable

/*
 * scope the policy set to be applicable to all authorization subscriptions "Fetch" actions on "measurements".
 */
for resource.type == "measurement"

/*
 * All doctors have full access to raw data
 */
policy "permit doctors raw data" 
permit subject.position == "DOCTOR"

/*
 * All nurses only get categrorised blood pressure data. For the first 20 seconds of each minute
 */
policy "nurses get categorised blood pressure first 20s" 
permit subject.position == "NURSE" & resource.monitorType == "BLOOD_PRESSURE"
where 
	time.secondOf(<time.now>) < 20; 
obligation "catrgorise blood pressure"

/*
 * All nurses get all raw data for the second 20 seconds of each minute
 */
policy "nurses get raw blood pressure 2nd 20s" 
permit subject.position == "NURSE" & resource.monitorType == "BLOOD_PRESSURE"
where 
	time.secondOf(<time.now>) < 40; 

/*
 * All nurses are denied data the last 20 seconds of each minute
 */
policy "nurses get denied blood pressure 3rd 20s" 
deny subject.position == "NURSE" & resource.monitorType == "BLOOD_PRESSURE"

/*
 * All nurses only get categrorised body temperature data.
 */
policy "nurses get categorised body temperature"
permit subject.position == "NURSE" & resource.monitorType == "BODY_TEMPERATURE"
obligation "categorise body temperature"


/* other data feeds raw for nurses */
policy "other data feeds raw for nurses" 
permit subject.position == "NURSE"

policy "deny others data feed access" 
deny
